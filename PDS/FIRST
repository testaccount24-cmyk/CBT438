         MACRO
&NAME    FIRST &VAR
         COPY  $MGBLDEF            BRING IN THE CLOWNS
         GBLC  &TRACE
&LCLV    SETC  '&VAR'              SET TO COUNT
         AIF   ('&VAR'(1,1) GE '0').NEXT010 GO IF SELF-DEFINING
&LCLV    SETC  '1'                 DO FIRST TIME ONLY
.NEXT010 ANOP
&$MDGLVL SETA  &$MDGLVL+1          INCREMENT LEVEL
&$MDGSEQ(&$MDGLVL) SETC '&SYSNDX'
&$MIFELS(&$MDGLVL) SETC 'F'
&$MDGNAM(&$MDGLVL) SETC '&NAME'
&$MCVERB(&$MDGLVL) SETC 'FIRST'
         AIF   ('&TRACE' NE 'YES').SKIP020
         DC    0C'$MDGLVL &$MDGLVL $MDGNAM &$MDGNAM(&$MDGLVL)'
.SKIP020 ANOP
&X       SETC  '$MDV&SYSNDX'       MAKE IT PRETTY
         AIF   ('&LCLV' NE '1').SKIP030
&NAME    TM    &X,X'01'            HAVE WE BEEN HERE BEFORE?
         JO    $MDF&SYSNDX         B IF YES
         OI    &X,X'01'            LEAVE TRACKS
DATA     LOCTR                     ENTER WORK AREA
&X       DC    X'00'               SET TO X'01' FIRST PASS
&SYSECT  LOCTR                     RESUME INSTRUCTION AREA
         MEXIT
.SKIP030 ANOP
&X       SETC  '$MDV&SYSNDX'       MAKE IT PRETTY
&NAME    CP    &X,=P'0'            FINISHED?
         JNH   $MDF&SYSNDX         B IF YES
         SP    &X,=P'1'            DECREMENT BY 1
DATA     LOCTR                     ENTER WORK AREA
&X       DC    P'&LCLV'            EXECUTION TIMES COUNTER
&SYSECT  LOCTR                     RESUME INSTRUCTION AREA
         MEND
