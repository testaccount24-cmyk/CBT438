         MACRO
&NAME    AFTER &VAR
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* AFTER 1 WILL CAUSE THE CODE FOLLOWING THIS MACRO TO BE EXECUTED ON *
.*            EVERY PASS EXCEPT THE FIRST PASS.                       *
.* AFTER N - EVERY PASS FOLLOWING THE N-TH PASS                       *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
         COPY  $MGBLDEF            BRING IN THE CLOWNS
&LCLV    SETC  '&VAR'              SET TO COUNT
         AIF   ('&VAR'(1,1) GE '0').OK30 GO IF SELF-DEFINING
         MNOTE '''&VAR'' NOT SELF-DEFINING: DO-GROUP WILL BE PROCESSED C
               WITH EACH PASS THROUGH THIS SECTION OF CODE'
&LCLV    SETC  '0'                 DO EVERY TIME
.OK30    ANOP
&$MDGLVL SETA  &$MDGLVL+1          INCREMENT LEVEL
&$MDGSEQ(&$MDGLVL) SETC '&SYSNDX'
&$MIFELS(&$MDGLVL) SETC 'F'
&$MDGNAM(&$MDGLVL) SETC '&NAME'
&$MCVERB(&$MDGLVL) SETC 'AFTER'
&X       SETC  '$MDV&SYSNDX'       MAKE IT PRETTY
         AIF   ('&LCLV' NE '1').PACKED
&NAME    TM    &X,X'01'            HAVE WE BEEN HERE BEFORE?
         JO    *+12                SKIP OVER IS YES
         OI    &X,X'01'            ELSE LEAVE TRACKS
         J     $MDF&SYSNDX           AND BYPASS DO-GROUP
         AGO   .DEFINE
.PACKED  ANOP                      I GUESS WE'LL HAVE TO DO DECIMAL...
&NAME    SP    &X,=P'1'            DECREMENT
         JNM   $MDF&SYSNDX         B IF NOT YET TIME
         PACK  &X,=Z'0'            DO EVERY TIME FROM NOW ON
.DEFINE  ANOP
DATA     LOCTR                     SET UP WORK AREA
         AIF   ('&LCLV' NE '1').PACKED2
&X       DC    X'00'               SET TO X'01' FIRST TIME THROUGH
         AGO   .INST
.PACKED2 ANOP
&X       DC    P'&LCLV'            FREQUENCY COUNTER
.INST    ANOP
&SYSECT  LOCTR                     BACK TO INSTRUCTION AREA
         MEND
