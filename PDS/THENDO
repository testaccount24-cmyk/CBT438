         MACRO
&NAME    THENDO &COND '&COND' MUST BE CHAR(S) FOLLOWING 'B' OF EXTENDED
.*             MNEMONICS, EG 'E' OF 'BE' OR 'NO' OF 'BNO', ETC
.*                                                                    *
.*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=**
.* 'THENDO' GROUPS MUST BE ENDED WITH 'ENDO' STATEMENT, AND MAY BE    *
.* NESTED AS DEEPLY AS NECESSARY (C'MON - WHO COULD EVER NEED MORE    *
.* THAN 255 LEVELS OF NESTING???).                                    *
.*                                                                    *
.* NOTE: TESTS ARE NOT MADE ON OPERAND TO ALLOW INSTALLATION TO CREATE*
.*       ADDITIONAL CONDITIONAL BRANCH INSTRUCTIONS VIA MACRO         *
.*       DEFINITIONS. EXAMPLE: 'BGE' FOR 'BRANCH ON GREATER THAN OR   *
.*       EQUAL TO' WHICH WOULD GENERATE THE SAME CODE AS 'BNL'.       *
.*                                                                    *
.*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=**
.*                                                                    *
         COPY  $MGBLDEF
         LCLC  &X,&Y
&Y       SETC  'N&COND'
         AIF   ('&COND'(1,1) NE 'N').SKIP05
&Y       SETC  '&COND'(2,1)
.SKIP05  ANOP
&$MDGLVL SETA  &$MDGLVL+1          UP DO-GROUP LEVEL
&$MDGSEQ(&$MDGLVL) SETC '&SYSNDX'  SET DO-GROUP SEQUENCE
&$MDGNAM(&$MDGLVL) SETC '&NAME'    SET DO-GROUP NAME
.*       DC    0C'$MDGLVL &$MDGLVL $MDGNAM &$MDGNAM(&$MDGLVL)'
&$MIFELS(&$MDGLVL) SETC 'F'
         AIF   ('&NAME' NE '').SKIP07
&$MDGNAM(&$MDGLVL) SETC '$MDG&$MDGSEQ(&$MDGLVL)'
.*       DC    0C'$MDGLVL &$MDGLVL $MDGNAM &$MDGNAM(&$MDGLVL)'
.SKIP07  J&Y   $MDF&$MDGSEQ(&$MDGLVL) SKIP DO-GROUP IF REL NOT TRUE
         AIF   ('&NAME' EQ '').SKIP10
&NAME    DC    0H'0'              NAME OF DO-GROUP
.SKIP10  ANOP
         MEND
